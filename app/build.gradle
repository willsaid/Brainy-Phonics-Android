buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion 27
    buildToolsVersion '28.0.2'
    defaultConfig {
        applicationId "com.hearatale.phonics"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 123
        versionName "1.2.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        renderscriptSupportModeEnabled true
        multiDexEnabled true

    }
    buildTypes {
        release {
            minifyEnabled false // true: proguard, false: otherwise
            shrinkResources false // true: proguard, false: otherwise
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${variant.applicationId}_${variant.versionName}_${variant.versionCode}.apk"
                }
            }
        }
    }

    flavorDimensions "development"

    productFlavors {
        development {
            resValue "string", "app_name", "Phonics"
            applicationId "com.hearatale.phonics.dev"
            buildConfigField 'String', 'ISDEVELOPING', '"working"'
            dimension "development"
        }

        production {
            resValue "string", "app_name", "Phonics"
            applicationId "com.hearatale.phonics.production"
            buildConfigField 'String', 'ISDEVELOPING', '"none"'
            dimension "development"
        }
    }

    final SUPPORT_LIBRARY_VERSION = '27.1.1'
    configurations.all {
        resolutionStrategy.force "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
        resolutionStrategy.force "com.android.support:animated-vector-drawable:$SUPPORT_LIBRARY_VERSION"
        resolutionStrategy.force "com.android.support:customtabs:$SUPPORT_LIBRARY_VERSION"
        exclude group: 'com.android.support', module: 'support-v4'
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    final SUPPORT_LIBRARY_VERSION = '27.1.1'
    final CONSTRAIN_LAYOUT_VERSION = '1.1.3'
    final GSON_VERSION = '2.8.5'
    final GLIDE_VERSION = '4.8.0'
    final BUTTER_KNIFE_VERSION = '8.8.1'
    final BRVAH_VERSION = '2.9.42'
    final EVENT_BUS_VERSION = '3.1.1'
    final CONFETTI = '1.1.1'
    final BLUR_VERSION = '2.1.1'
    final FABRIC_VERSION = '2.9.5'


    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // support library
    implementation("com.android.support:design:$SUPPORT_LIBRARY_VERSION") {
        exclude module: 'support-media-compat'
    }
    implementation "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"

    implementation "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"

    // constraint layout
    implementation "com.android.support.constraint:constraint-layout:$CONSTRAIN_LAYOUT_VERSION"

    // gson
    implementation "com.google.code.gson:gson:$GSON_VERSION"

    // glide
    implementation "com.github.bumptech.glide:glide:$GLIDE_VERSION"
    annotationProcessor "com.github.bumptech.glide:compiler:$GLIDE_VERSION"

    // butter knife
    implementation "com.jakewharton:butterknife:$BUTTER_KNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTER_KNIFE_VERSION"

    // bravh
    implementation "com.github.CymChad:BaseRecyclerViewAdapterHelper:$BRVAH_VERSION"

    // event bus
    implementation "org.greenrobot:eventbus:$EVENT_BUS_VERSION"

    //cardview
    implementation "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"

    //confetti for spawn animation (raining coins)
    implementation "com.github.jinatonic.confetti:confetti:$CONFETTI"

    // blur
    implementation "jp.wasabeef:blurry:$BLUR_VERSION"

    //transition every where
    implementation "com.andkulikov:transitionseverywhere:1.8.0"

    // fabric
    implementation("com.crashlytics.sdk.android:crashlytics:$FABRIC_VERSION") {
        transitive = true;
    }


}
